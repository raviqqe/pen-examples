import Core'Number
import Core'String
import Os'Context { Context }
import Os'Directory
import Os'Environment
import Os'File
import Os'File'OpenOptions { OpenOptions }

f = \() none | error {
  error("oh no")
}

g = \(x number) number | error {
  if f()? == none {
    0
  } else {
    42
  }
}

h = \(x number | error) number | error {
  x? + 1
}

i = \(xs [number]) [number | none] {
  [number | none ...xs]
}

command = \(ctx Context) none | error {
  File'Write(ctx, File'StdOut(), "Hello, world!\n")?

  f = File'Open(ctx, "./pen.json")?
  s = File'Read(ctx, f)?
  f = File'OpenWithOptions(
    ctx,
    "foo.json",
    OpenOptions{
      ...OpenOptions'Default(),
      Create: true,
      Write: true,
      Truncate: true,
    },
  )?
  File'Write(ctx, f, s)?
  File'Write(ctx, File'StdOut(), s)?
  File'Write(ctx, File'StdOut(), String'Join(Directory'Read(ctx, ".")?, "\n"))?
  Directory'Create(ctx, "foo")?
  Directory'Remove(ctx, "foo")?
  v = Environment'Variable(ctx, "PATH")?
  File'Write(
    ctx,
    File'StdOut(),
    String'Join([string v, "\n"], ""),
  )?

  none
}

main = \(ctx Context) number {
  ctx = ctx.Os

  if e = command(ctx) as none {
    0
  } else {
    debug(e)

    1
  }
}
